set terminal epslatex size 8cm, 6cm standalone
# set terminal png size 1200,900
set termoption dash

# Line style for axes
set style line 80 lt rgb "#808080"

# Line style for grid
set style line 81 lt 0  # dashed
set style line 81 lt rgb "#808080"  # grey

set grid back linestyle 81
set border 3 back linestyle 80 # Remove border on top and right.  These
             # borders are useless and make it harder
             # to see plotted lines near the border.
    # Also, put it in grey; no need for so much emphasis on a border.
set xtics nomirror
set ytics nomirror

#set log x
#set mxtics 10    # Makes logscale look good.

#set for [i=11:16] linetype i dt i

# Line styles: try to pick pleasing colors, rather
# than strictly primary colors or hard-to-see colors
# like gnuplot's default yellow.  Make the lines thick
# so they're easy to see in small plots in papers.
set style line 1 lt rgb "#AA0000" lw 4 pt 1
set style line 2 lt rgb "#00AA00" lw 4 pt 6
set style line 3 lt rgb "#5555DD" lw 4 pt 2
set style line 4 lt rgb "#FF5500" lw 4 pt 9
set style line 5 lt rgb "#00AA55" lw 4 pt 5
set style line 6 lt rgb "#0000A0" lw 4 pt 4

set style line 11 lt rgb "#AA0000" lw 4
set style line 12 lt rgb "#00AA00" lw 4
set style line 13 lt rgb "#5555DD" lw 4
set style line 14 lt rgb "#FF5500" lw 4
set style line 15 lt rgb "#00AA55" lw 4
set style line 16 lt rgb "#0000A0" lw 4
set style line 17 lt rgb "#023FA5" lw 4
set style line 18 lt rgb "#8E063B" lw 4
set style line 19 lt rgb "#4A6FE3" lw 4
set style line 20 lt rgb "#D33F6A" lw 4
set style line 21 lt rgb "#12C638" lw 4
set style line 22 lt rgb "#EF9708" lw 4
set style line 23 lt rgb "#0FCFC0" lw 4
set style line 24 lt rgb "#F79CD4" lw 4

# {2,63,165}    "#023FA5"
# {142,6,59},   "#8E063B"
# {74,111,227}  "#4A6FE3"
# {211,63,106}, "#D33F6A"
# {17,198,56}   "#12C638"
# {239,151,8},  "#EF9708"
# {15,207,192}  "#0FCFC0"
# {247,156,212} "#F79CD4"

string_p = 'plots/'
string_o = 'svm_output_data/'

set output string_p.mname.'.png'

set xlabel 'r$\epsilon$'
set ylabel '\small{Adversarial success (\%)}' offset 2
set xrange [0:2.5]
set yrange [0:1.0]
# set margins 3, 1, 2, 2
set key right bottom
set title 'Retrain defense on '.mname

plot for [IDX=0:6] string_o.mname.'.txt' i (2*IDX) using 1:4 with lines linestyle (IDX+1) title columnheader(1)

unset output
